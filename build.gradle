// gradle build script for the arc42 template family
//
// free software - without guarantee, use at your own risk
// ========================================================


buildscript {
    // these are the BUILDSCRIPT deps - required to execute
    // build targets and -operations
    repositories {
        maven {
            name 'Bintray Asciidoctor repo'
            url  'http://dl.bintray.com/content/aalmiray/asciidoctor'
        }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath( 'org.asciidoctor:asciidoctor-gradle-plugin:0.7.0' )

    }
}


// set common output directory for all subprojects
project.ext.globalBuildDir = project.buildDir

task createTemplateWithoutHelp(
    description: 'takes the golden master and removes all help sections'
) {
    def sourcePath = './src/DE/asciidoc/'
    def targetPath = './src_gen/DE/asciidoc/'
	def source = new File(sourcePath+'golden-master/src/.')
    //the list of features/tags available in the golden master
    def allFeatures = ['help','example']
    //the template styles with their corresponding features
    def templateStyles = [
                        'plain':[],
                        'with-help':['help'],
                        'with-examples':['help','example'],
                    ]
    templateStyles.each { templateName, featuresWanted ->
        def featuresToRemove = allFeatures - featuresWanted
        def target = new File(targetPath+templateName+'/src/.')
        target.mkdirs()
        source.eachFile { file ->
            if (file.name.endsWith('.adoc')) {
                def targetFile = new File(target.path+'/'+file.name)
                println targetFile.path
                def template = file.text
                featuresToRemove.each { feature ->
                    template = template.replaceAll(/(?ms)\[role="arc42/+feature+/"\][ \r\n]+[*]{4}.*?[*]{4}/,'')
                }
                targetFile.write(template)
            }
        }
    }      
}

